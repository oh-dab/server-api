plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.14-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'com.diffplug.spotless' version '6.11.0'
	id "org.asciidoctor.jvm.convert" version "3.3.2"
}

java {
	sourceCompatibility = '11'
}

bootJar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	outputs.dir snippetsDir
}

asciidoctor {
	inputs.dir snippetsDir
	configurations 'asciidoctorExtensions'
	dependsOn test
}

configurations {
	asciidoctorExtensions
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
	implementation project(':core-module')
	implementation project(':entity-module')

	implementation project(':member-module')
	implementation project(':member-module:member-web-adapter')
	implementation project(':member-module:member-application')
	implementation project(':member-module:member-persistence-adapter')

	implementation project(':teacher-module')
	implementation project(':teacher-module:teacher-web-adapter')
	implementation project(':teacher-module:teacher-application')
	implementation project(':teacher-module:teacher-persistence-adapter')

	implementation project(':wronganswernote-module')
	implementation project(':wronganswernote-module:wronganswernote-web-adapter')
	implementation project(':wronganswernote-module:wronganswernote-application')
	implementation project(':wronganswernote-module:wronganswernote-persistence-adapter')

	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.h2database:h2'
	asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

spotless {
	java {
		target("**/*.java")
		removeUnusedImports()
		googleJavaFormat().aosp()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

subprojects {
	group = 'com'
	version = '0.0.1-SNAPSHOT'

	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'java-library'

	jar {
		enabled = true
	}

	java {
		sourceCompatibility = '11'
	}


	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/milestone' }
		maven { url 'https://repo.spring.io/snapshot' }
	}

	dependencyManagement {
		imports {
			mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-security'
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.security:spring-security-test'
	}

	test {
		useJUnitPlatform()
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

project(':member-module') {
	dependencies {
		implementation project(':core-module')
		implementation project(':entity-module')
		implementation project(':member-module:member-web-adapter')
		implementation project(':member-module:member-application')
		implementation project(':member-module:member-persistence-adapter')
	}
}

project(':teacher-module') {
	dependencies {
		implementation project(':core-module')
		implementation project(':entity-module')
		implementation project(':teacher-module:teacher-web-adapter')
		implementation project(':teacher-module:teacher-application')
		implementation project(':teacher-module:teacher-persistence-adapter')
	}
}

project(':wronganswernote-module') {
	dependencies {
		implementation project(':core-module')
		implementation project(':entity-module')
		implementation project(':wronganswernote-module:wronganswernote-web-adapter')
		implementation project(':wronganswernote-module:wronganswernote-application')
		implementation project(':wronganswernote-module:wronganswernote-persistence-adapter')
	}
}

project(':core-module') {
	dependencies {
		implementation("io.jsonwebtoken:jjwt-api:0.11.5")
		runtimeOnly("io.jsonwebtoken:jjwt-impl:0.11.5")
		runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.11.5")
	}
}